apply plugin: 'com.android.application'

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    // Mapsforge / VTM online
    implementation 'org.mapsforge:vtm-android:master-SNAPSHOT'
    implementation 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-armeabi'
    implementation 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-armeabi-v7a'
    implementation 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-x86'
    implementation 'org.mapsforge:vtm-themes:master-SNAPSHOT'

    implementation 'org.mapsforge:mapsforge-core:master-SNAPSHOT'
    implementation 'org.mapsforge:mapsforge-poi-android:master-SNAPSHOT'
    implementation 'org.mapsforge:spatialite-android:master-SNAPSHOT:natives-armeabi'
    implementation 'org.mapsforge:spatialite-android:master-SNAPSHOT:natives-armeabi-v7a'
    implementation 'org.mapsforge:spatialite-android:master-SNAPSHOT:natives-x86'

    // Android
    implementation "org.slf4j:slf4j-android:$slf4jVersion"
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.android.support:support-v4:27.1.0'
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Graphhopper
    implementation(group: 'com.graphhopper', name: 'graphhopper-core', version: '0.10-SNAPSHOT') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'org.openstreetmap.osmosis', module: 'osmosis-osm-binary'
        exclude group: 'org.apache.xmlgraphics', module: 'xmlgraphics-commons'
    }
    implementation 'com.graphhopper:graphhopper-reader-osm:0.10-SNAPSHOT'
    // implementation 'com.graphhopper:graphhopper-reader-overlay-data:0.+'

    implementation 'com.github.bmelnychuk:atv:1.2.9' // TreeView helper
    implementation 'com.github.johnkil.print:print:1.3.1' // PrintView
    implementation 'com.github.woxthebox:draglistview:1.4.3' // Drag n Drop helper
}

android {
    compileSdkVersion androidCompileSdk()
    buildToolsVersion "$androidBuildVersionTools"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        versionCode versionCode()
        versionName versionName()
        minSdkVersion androidMinSdk()
        targetSdkVersion androidTargetSdk()
        vectorDrawables.useSupportLibrary = true // Needed to support vector drawables below API 21
        multiDexEnabled false // true if too more than 64k methods are included
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions { abortOnError false }

    packagingOptions {
        exclude 'META-INF/services/org.jeo.data.Driver'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    buildTypes {
        all {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
